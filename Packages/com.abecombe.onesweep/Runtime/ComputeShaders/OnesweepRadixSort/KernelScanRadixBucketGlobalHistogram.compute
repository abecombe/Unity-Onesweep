// This file includes code adapted from GPUSorting by Thomas Smith
// https://github.com/b0nes164/GPUSorting
// Licensed under the MIT License
// Modified by abecombe, 2025

#pragma kernel ScanRadixBucketGlobalHistogram

#pragma use_dxc
#pragma require wavebasic
#pragma require waveballot
#pragma multi_compile WAVE_SIZE_32 WAVE_SIZE_64
#pragma multi_compile KEY_TYPE_UINT KEY_TYPE_INT KEY_TYPE_FLOAT
#pragma multi_compile SORTING_ORDER_ASCENDING SORTING_ORDER_DESCENDING
#pragma multi_compile USE_DIRECT_DISPATCH USE_INDIRECT_DISPATCH

#include "Radix.hlsl"
#include "PartitionDescriptor.hlsl"
#include "../OnesweepCommon/Wave.hlsl"
#include "DispatchUtils.hlsl"

#define THREADS_PER_GROUP (RADIX_BASE)

StructuredBuffer<uint> bucket_count_buffer; // size: RADIX_BASE * RADIX_STEP_COUNT
RWStructuredBuffer<PARTITION_DESCRIPTOR> partition_descriptor_buffer; // size: RADIX_BASE * group_count * RADIX_STEP_COUNT

groupshared uint group_shared[RADIX_BASE];

/**
 * \brief Performs a scan (prefix sum) over the global radix histogram buffer.
 *
 * \note Dispatch group size: RADIX_STEP_COUNT
 *
 * This shader scans the per-radix global bucket counts (one group per radix value),
 * converting them into exclusive prefix sums. The results are written into a partition
 * descriptor buffer used for sorting steps.
 */
[numthreads(THREADS_PER_GROUP, 1, 1)]
void ScanRadixBucketGlobalHistogram(uint group_thread_id : SV_GroupThreadID, uint group_id : SV_GroupID)
{
#if defined(USE_INDIRECT_DISPATCH)
    const uint group_count = get_group_count();
#endif

    uint bucket_count = bucket_count_buffer[group_thread_id + group_id * RADIX_BASE];

    bucket_count += WavePrefixSum(bucket_count); // inclusive scan
    // ((LANE_INDEX + 1u) & WAVE_SIZE_MASK) + (group_thread_id & ~WAVE_SIZE_MASK) means
    // 1, 2, .. , 31, 0, 33, 34, .. , 63, 32, 65, 66, .. , 95, 64, ...
    group_shared[((LANE_INDEX + 1u) & WAVE_SIZE_MASK) + (group_thread_id & ~WAVE_SIZE_MASK)] = bucket_count;

    GroupMemoryBarrierWithGroupSync();

    if (group_thread_id < WAVE_COUNT_IN_GROUP(THREADS_PER_GROUP))
        group_shared[group_thread_id * WAVE_SIZE] = WavePrefixSum(group_shared[group_thread_id * WAVE_SIZE]);

    GroupMemoryBarrierWithGroupSync();

    uint scan_bucket_count = group_shared[group_thread_id];
    if (LANE_INDEX != 0u && group_thread_id >= WAVE_SIZE)
        scan_bucket_count += group_shared[WAVE_INDEX(group_thread_id) * WAVE_SIZE];

    partition_descriptor_buffer[get_partition_descriptor_buffer_address(group_thread_id, 0, group_count, group_id)]
        = create_partition_descriptor(scan_bucket_count, FLAG_INVALID);
}